class NDoomRocket extends Rocket replaces Rocket
{
    set Gravity to 0.0235;
    set Speed to 14;
    set Damage to 40; // direct hit damage

    label Spawn
    {
        TNT1 A 0 {
            x6 A_SpawnItemEx("NRocketFire", -cos(angle) * 3, -sin(angle) * 3, -velz, FRandom(-1, 1), FRandom(-1, 1), FRandom(-1, 1));
            A_SpawnItemEx("NRocketCorona");
            A_ScaleVelocity(1.25);
        };
        x10 MISL A 0 [Bright] A_SpawnItemEx("NRocketTrail", -cos(angle) * 20, -sin(angle) * 20, -velz, FRandom(-5, 5) + cos(angle) * 2, FRandom(-5, 5) + sin(angle) * 2, FRandom(-5, 5));
        MISL A 2 [Bright];
        loop;
    };

    label Death
    {
        TNT1 A 0 A_QuakeEx(4, 4, 2.25, 16, 0, 128);
        x120 TNT1 A 0 A_SpawnItemEx("NRocketTrail", FRandom(-20, 20), FRandom(-20, 20), FRandom(-20, 20), FRandom(-6, 6) - velx, FRandom(-6, 6) - vely, FRandom(-6, 6));
        MISL B 5 [Bright] A_Explode(150, 210);
        MISL C 5 [Bright];
        MISL D 5 [Bright];
        stop;
    };
}

class NRocketFX
{
    set Gravity to 0.0082;
    set Speed to 9;
    set Alpha to 0.35;
    set RenderStyle to Translucent;
    combo Projectile;
}

class NRocketFire extends NRocketFX
{
    set Scale to 0.2;
    set Alpha to 0.8;
    set RenderStyle to Add;

    label Spawn
    {
        FSPK ABC 2 [Bright];
        stop;
    };
}

class NRocketTrail extends NRocketFX
{
    set Scale to 0.55;

    label Spawn
    {
        RTRL ABCD 6 [Bright];
        stop;
    };
}